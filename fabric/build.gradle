plugins {
    id 'com.github.johnrengelman.shadow'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${project.fabric_loader_version}")

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}")

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    //Energy
    include modApi("teamreborn:energy:${project.fabric_energy_version}")

    //Xaero's Minimap
    modLocalRuntime "curse.maven:xaeros-minimap-263420:${project.xaeros_minimap_fabric_version}"

    //JourneyMap
    modCompileOnly "curse.maven:journeymap-32274:${project.journeymap_fabric_version}"

    //FTB Chunks + Dependencies
    modCompileOnly "curse.maven:ftb-chunks-fabric-472657:${project.ftb_chunks_fabric_version}"
    modCompileOnly "curse.maven:ftb-library-fabric-438495:${project.ftb_library_fabric_version}"
    modCompileOnly "curse.maven:ftb-teams-fabric-438497:${project.ftb_teams_fabric_version}"
    modCompileOnly "curse.maven:architectury-api-419699:${project.architectury_api_fabric_version}"

    //Trinkets + Accessories
    modCompileOnly "curse.maven:accessories-938917:${project.accessories_fabric_version}"
    modCompileOnly "curse.maven:accessories-tc-layer-1005680:${project.tclayer_fabric_version}"
    modCompileOnly "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-item:5.2.1"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1"

    // Development QOL
    modLocalRuntime("maven.modrinth:lazydfu:${lazydfu_version}")
    modLocalRuntime("com.terraformersmc:modmenu:${modmenu_version}")

    // Recipe Viewers - Create Fabric supports JEI, REI, and EMI.
    // See root gradle.properties to choose which to use at runtime.
    switch (fabric_recipe_viewer.toLowerCase(Locale.ROOT)) {
        case "jei": modLocalRuntime("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"); break
        case "rei": modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"); break
        case "emi": modLocalRuntime("dev.emi:emi:${emi_version}"); break
        case "disabled": break
        default: println("Unknown recipe viewer specified: ${fabric_recipe_viewer}. Must be JEI, REI, EMI, or disabled.")
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_project_id
    versionNumber = minecraft_version + "-" + version
    versionName = base.archivesName.get() + "-" + version
    versionType = "release"
    uploadFile = remapJar

    List<String> mcVersions = minecraft_game_versions.split(",")
    gameVersions = mcVersions
    loaders = ["fabric"]

    changelog = recentChangelogText

    dependencies {
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
        optional.project "journeymap"
        optional.project "trinkets"
        optional.project "accessories"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_project_id, remapJar)
    mainFile.changelogType = "markdown"
    mainFile.changelog = recentChangelogText
    mainFile.releaseType = "release"

    List<String> mcVersions = minecraft_game_versions.split(",")
    mainFile.gameVersions = mcVersions
    mainFile.addModLoader("Fabric")
    mainFile.addJavaVersion("Java 17")

    mainFile.addOptional("xaeros-minimap")
    mainFile.addOptional("xaeros-minimap-fair-play-edition")
    mainFile.addOptional("journeymap")
    mainFile.addOptional("ftb-chunks-fabric")
    mainFile.addOptional("trinkets")
    mainFile.addOptional("accessories")
}

task publishFabric {
    group = "publishing"
    afterEvaluate {
        dependsOn(tasks.publishCurseForge)
        dependsOn(tasks.modrinth)
    }
}