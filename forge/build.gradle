plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "pocketgps.mixins.json"
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    //Xaero's Minimap
    compileOnly "curse.maven:xaeros-minimap-263420:${project.xaeros_minimap_forge_version}"

    //JourneyMap
    compileOnly "curse.maven:journeymap-32274:${project.journeymap_forge_version}"

    //FTB Chunks + Dependencies
    compileOnly "curse.maven:ftb-chunks-forge-314906:${project.ftb_chunks_forge_version}"
    compileOnly "curse.maven:ftb-library-forge-404465:${project.ftb_library_forge_version}"
    compileOnly "curse.maven:ftb-teams-forge-404468:${project.ftb_teams_forge_version}"
    compileOnly "curse.maven:architectury-api-419699:${project.architectury_api_forge_version}"

    //Curios + Accessories
    compileOnly "curse.maven:accessories-938917:${project.accessories_forge_version}"
    compileOnly "curse.maven:accessories-cc-layer-1005683:${project.cclayer_forge_version}"
    compileOnly "me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}"
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
