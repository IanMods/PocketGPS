plugins {
    id 'com.github.johnrengelman.shadow'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}

loom {
    forge {
        mixinConfig "pocketgps.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modLocalRuntime "maven.modrinth:mekanism:${project.mekanism_version}"
    modLocalRuntime "maven.modrinth:mekanism-generators:${project.mekanism_version}"

    //Xaero's Minimap
    modCompileOnly "maven.modrinth:xaeros-minimap:${project.xaeros_minimap_version}_Forge_1.20"

    //JourneyMap
    modCompileOnly "maven.modrinth:journeymap:${project.minecraft_version}-${project.journeymap_version}-forge"

    //FTB Chunks + Dependencies
    modCompileOnly "curse.maven:ftb-chunks-forge-314906:${project.ftb_chunks_forge_version}"
    modCompileOnly "curse.maven:ftb-library-forge-404465:${project.ftb_library_forge_version}"
    modCompileOnly "curse.maven:ftb-teams-forge-404468:${project.ftb_teams_forge_version}"
    modCompileOnly "maven.modrinth:architectury-api:${project.architectury_version}+forge"

    //Curios + Accessories
    modCompileOnly "io.wispforest:accessories-neoforge:${project.accessories_version}"
    modCompileOnly("me.shedaniel.cloth:cloth-config-forge:${project.cloth_config_version}") {
        exclude group: 'net.fabricmc', module: 'fabric-loader'
    }
    modCompileOnly "maven.modrinth:accessories-cc-layer:${project.curios_version}"
//    modImplementation "maven.modrinth:curios:5.10.0+1.20.1"

    // Development QOL
    modLocalRuntime("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei_version}") { transitive = false }
//    modImplementation "curse.maven:item-nbt-viewer-514135:4578522"

    // Minimap Mod Dev Tests
    // See root gradle.properties to choose which to use at runtime.
    switch (project.minimap_mod.toLowerCase(Locale.ROOT)) {
        case "xaero":
            modLocalRuntime "maven.modrinth:xaeros-minimap:${project.xaeros_minimap_version}_Forge_1.20"
            break
        case "journey":
            modLocalRuntime "maven.modrinth:journeymap:${project.minecraft_version}-${project.journeymap_version}-forge"
            break
        case "ftb":
            modLocalRuntime "curse.maven:ftb-chunks-forge-314906:${project.ftb_chunks_forge_version}"
            modLocalRuntime "curse.maven:ftb-library-forge-404465:${project.ftb_library_forge_version}"
            modLocalRuntime "curse.maven:ftb-teams-forge-404468:${project.ftb_teams_forge_version}"
            modLocalRuntime "maven.modrinth:architectury-api:${project.architectury_version}+forge"
            break
        case "none":
            break
        default: println("Unknown recipe viewer specified: ${project.minimap_mod}. Must be xaero, journey, ftb, " +
                "atlas, or none")
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

String changelogText = projectDir.toPath().getParent().resolve("CHANGELOG.md").getText('UTF-8')

modrinth {
    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_project_id
    versionNumber = minecraft_version + "-" + version
    versionName = base.archivesName.get() + "-" + mod_version
    versionType = "release"
    uploadFile = remapJar

    List<String> mcVersions = minecraft_game_versions.split(",")
    gameVersions = mcVersions
    loaders = ["forge", "neoforge"]

    changelog = recentChangelogText

    dependencies {
        optional.project "xaeros-minimap"
        optional.project "xaeros-minimap-fair"
        optional.project "journeymap"
        optional.project "curios"
        optional.project "accessories"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "curseforge"

    int firstHeading = changelogText.indexOf("## [")
    int nextHeading = changelogText.indexOf("## [", firstHeading + 1)
    String recentChangelogText = changelogText.substring(firstHeading, nextHeading)

    debugMode = false
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforge_project_id, remapJar)
    mainFile.changelogType = "markdown"
    mainFile.changelog = recentChangelogText
    mainFile.releaseType = "release"

    List<String> mcVersions = minecraft_game_versions.split(",")
    mainFile.gameVersions = mcVersions
    mainFile.addModLoader("Forge")
    mainFile.addModLoader("NeoForge")
    mainFile.addJavaVersion("Java 17")

    mainFile.addOptional("xaeros-minimap")
    mainFile.addOptional("xaeros-minimap-fair-play-edition")
    mainFile.addOptional("journeymap")
    mainFile.addOptional("ftb-chunks-forge")
    mainFile.addOptional("curios")
    mainFile.addOptional("accessories")
}

task publishForge {
    group = "upload"
    afterEvaluate {
        dependsOn(tasks.publishCurseForge)
        dependsOn(tasks.modrinth)
    }
}